<!DOCTYPE html>
<html>
  <head>
    <title>Gamepad Demo</title>
    <script src="../browser/flowcopter.js"></script>
    <script type="application/fbp">
###
# WebSocket connection
'flowcopter' -> PROTOCOL Connect(websocket/Connect)
'ws://localhost:8000' -> URL Connect()
Connect() CONNECTION -> CONNECTION Send(websocket/SendMessage)
Connect() ERROR -> IN Show(core/Output)

###
# MainData Graph
'0' -> STRING SendString(strings/SendString)
'20' -> INTERVAL RunInterval(core/RunInterval)
'axes' -> KEY ReadAxes(objects/ExtractProperty)
'buttons' -> KEY ReadButtons(objects/ExtractProperty)
RunInterval() OUT -> IN SendString(strings/SendString)
SendString(strings/SendString) OUT -> GAMEPAD ReadGamepad(interaction/ReadGamepad)
ReadGamepad() OUT -> IN GamepadData(core/Split)
GamepadData() OUT -> IN ReadAxes()
GamepadData() OUT -> IN ReadButtons()
ReadAxes() OUT -> IN GetAxes(core/Split) OUT -> IN Drop(core/Drop)
ReadButtons() OUT -> IN GetButtons(core/Split)

###
# takeoff Command
'takeoff' -> NAME TakeOffCommand(flowcopter/WireCommand)
'9' -> BUTTON GetStartButton(flowcopter/GamepadButton)
GetButtons() OUT -> IN GetStartButton()
GetStartButton() OUT -> VALUE TakeOffCommand()
TakeOffCommand OUT -> IN MergeCommands(core/Merge)

###
# land Command
'land' -> NAME LandCommand(flowcopter/WireCommand)
'8' -> BUTTON GetBackButton(flowcopter/GamepadButton)
GetButtons() OUT -> IN GetBackButton()
GetBackButton() OUT -> VALUE LandCommand()
LandCommand OUT -> IN MergeCommands()

MergeCommands() OUT -> IN Show(core/Output) OUT -> IN Stringify(strings/Jsonify)
Stringify() OUT -> STRING Send()
</script>
    <script>
      var noflo = require('noflo');
      var fbp = document.querySelector('script[type="application/fbp"]');
      var fbpString = fbp.innerText.trim();
      noflo.graph.loadFBP(fbpString, function (graph) {
        graph.baseDir = '/flowcopter';
        noflo.createNetwork(graph);
      });
    </script>
  </head>
  <body>


  </body>
</html>
